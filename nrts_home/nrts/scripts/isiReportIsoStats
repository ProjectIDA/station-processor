#!/bin/csh -f
# $Id: isiReportIsoStats,v 1.2 2005/10/12 00:25:52 dechavez Exp $
#
# Print summary of iso stats

set myname = isiReportIsoStats

# General setup

set setup  = ~nrts/scripts/ScriptSetup
if (! -e $setup ) then
    echo "missing $setup"
    goto failure
endif
source $setup

# Command line setup

set error = 0
if ($#argv == 0) then
    set site = $DefaultSite
else if ($#argv == 1) then
    set site = $argv[1]
else
    set error = 1
endif

if ($error) then
    echo "usage: $myname [ site ]"
    goto failure
endif

# Get the full path names

set SiteDir    = $NRTS_HOME/$site
set IsoDir     = $SiteDir/iso
set ConfigFile = $IsoDir/config
set StageDir   = $IsoDir/stage

# Nothing to do if there isn't a complete ISO tree

if (! -d $IsoDir) then
    echo "missing ISO dir $IsoDir"
    goto failure
endif

if (! -e $ConfigFile) then
    echo "missing ISO config file $ConfigFile"
    goto failure
endif

if (! -e $StageDir) then
    echo "missing $StageDir"
    goto failure
endif

source $ConfigFile
set capacity = $DefaultMaxMbytes

# Get the stats

@ bytes  = `du -sk $StageDir | awk '{print $1}'`
@ bytes /= 1024

set files = `ls $StageDir | grep ".gz" | wc -l`
set images = `ls $IsoDir | grep ".iso" | wc -l`
@ percent = (100 * $bytes) / $capacity

echo "${site}: $bytes Mbytes ($percent%) staged in $files files [ $images ISO images ready to burn ]"

success:
exit 0

failure:
exit 1

# Revision History
#
# $Log: isiReportIsoStats,v $
# Revision 1.2  2005/10/12 00:25:52  dechavez
# include site name in report line
#
# Revision 1.1  2005/10/12 00:14:05  dechavez
# initial release
#
