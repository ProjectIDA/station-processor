#!/bin/csh -f
# $Id: isiFlushBuffer,v 1.1 2005/08/17 21:06:56 dechavez Exp $

if ($#argv == 0) then
    set site = UNDEFINED
else if ($#argv == 1) then
    set site = $argv[1]
else
    echo "usage: $myname [ SiteName ]"
    exit 1
endif

# General setup

set myname = isiFlushBuffer

set setup  = ~nrts/scripts/ScriptSetup
if (! -e $setup ) then
    echo "${myname}: missing $setup"
    exit 1
endif
source $setup

# Set site name if not explicitly given on the command line

if ($site == UNDEFINED) then
    set site = `nrtsDefaultSystem`
    if ($status != 0) then
        echo "Unable to determine default site name"
        exit 1
    endif
endif

set StartingDir = $NRTS_HOME/$site
if (-d $StartingDir) then
    cd $StartingDir
    if ($status != 0) then
        echo "Unable to chdir to $StartingDir"
        exit 1
    endif
else
    echo "Missing $StartingDir"
    exit 1
endif

isiStageZipFiles FORCE $site
if ($status != 0) then
    echo "unable to force ISO image"
    exit 1
endif

set count = `CountIsoImages $StartingDir/iso`
echo "$count ISO images ready to burn"

exit 0

# Revision History
#
# $Log: isiFlushBuffer,v $
# Revision 1.1  2005/08/17 21:06:56  dechavez
# initial release
#
