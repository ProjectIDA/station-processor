#!/bin/csh -f
# $Id: isiProcessTeeFiles,v 1.3 2005/09/15 17:09:53 dechavez Exp $
#
# Process all tee files generated by the ISI

set debug  = 0
set myname = isiProcessTeeFiles

if ($debug) logger -t $myname"[$$]" -p user.info "begin"

# Make sure no other instance of this script is currently running

ps -ef | fgrep $myname | fgrep -v fgrep | fgrep -v $$ | fgrep -v "sh -c" > /dev/null
if ($status == 0) then
    if ($debug) logger -t $myname"[$$]" -p user.info "$myname instance already running"
    exit 0
endif

# General setup

set setup  = ~nrts/scripts/ScriptSetup
if (! -e $setup ) then
    set message = "missing $setup"
    logger -t $myname"[$$]" -p user.info "$message"
    echo "${myname}: $message"
    goto failure
endif
source $setup

# Process each site in turn

if ($SystemsCount == 0) then
    if ($debug) logger -t $myname"[$$]" -p user.info "SystemCount is zero"
    goto success
endif

set SiteList = ""
foreach site ($SystemsList)

    # Nothing to do if there isn't a tee directory

    set SiteDir = $NRTS_HOME/$site
    set TeeDir = $SiteDir/tee
    if (! -d $TeeDir) continue

    # If there is also and iso directory, insure that the whole tree is there

    set IsoDir = $SiteDir/iso
    if ( -d $IsoDir) isiBuildIsoTree $SiteDir

    # Make the site our working directory

    cd $SiteDir
    if ($status != 0) then
        logger -t $myname"[$$]" -p user.info "can't cd to $SiteDir"
        goto failure
    endif

    # Compress all inactive tee files

    set command = isiCompressTeeFiles
    $command
    if ($status != 0) then
        logger -t $myname"[$$]" -p user.info "$command FAILED"
        goto failure
    endif

    # Distribute them

    set command = isiDistributeZipFiles
    $command
    if ($status != 0) then
        logger -t $myname"[$$]" -p user.info "$command FAILED"
        goto failure
    endif

    # Drive the ISO image generation

    set command = "isiStageZipFiles $site"
    $command
    if ($status != 0) then
        logger -t $myname"[$$]" -p user.info "$command FAILED"
        goto failure
    endif

end

# Normal exit

success:
    if ($debug) logger -t $myname"[$$]" -p user.info "completed OK"
    exit 0

# Error exit

failure:
    logger -t $myname"[$$]" -p user.info "FAILED"
    exit 1

# Revision History
#
# $Log: isiProcessTeeFiles,v $
# Revision 1.3  2005/09/15 17:09:53  dechavez
# remove call to (non-existant) isiNoteFailure
#
# Revision 1.2  2005/09/13 15:39:33  dechavez
# exit immediatly if already running (no more goto done)
#
# Revision 1.1  2005/08/17 21:06:56  dechavez
# initial release
#
