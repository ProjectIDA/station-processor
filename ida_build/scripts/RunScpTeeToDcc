#!/bin/csh -f
# $Id: RunScpTeeToDcc,v 1.5 2008/11/04 19:07:00 dechavez Exp $
#
# wrapper to ScpToDcc, runs it for all entries in the Systems file

set debug  = 0
set myname = RunScpTeeToDcc

if ($debug) logger -t $myname"[$$]" -p user.info "begin"

if ($#argv == 1) then
    set DestPrefix = $argv[1]
else if ($#argv == 2) then
    set DestPrefix  = $argv[1]
    set DestPrefix2 = $argv[2]
else
    logger -t $myname"[$$]" -p user.info "bad command line"
    goto failure
endif


# General setup

set setup = ~nrts/scripts/ScriptSetup
if (! -e $setup ) then
    set message = "missing $setup"
    logger -t $myname"[$$]" -p user.info "$message"
    echo "${myname}: $message"
    goto failure
endif
source $setup

# Process each site listed in the Systems file

set systems = /usr/nrts/etc/Systems
if (! -e $systems) then
    logger -t $myname"[$$]" -p user.info "missing $systems file"
    goto failure
endif

set SiteList = `stripcat $systems`
if ($status != 0) then
    logger -t $myname"[$$]" -p user.info "stripcat $systems FAILED"
    goto failure
endif

foreach site ($SiteList)
    set dest = $DestPrefix/$site
    if ($?DestPrefix2) set dest = "$dest $DestPrefix2/$site"
    set command = "ScpTeeToDcc $site $dest"
    if ($debug) logger -t $myname"[$$]" -p user.info "$command"
    $command
end

# Normal exit

success:
pkill -HUP -x ispd
if ($debug) logger -t $myname"[$$]" -p user.info "completed OK"
exit 0

failure:
pkill -HUP -x ispd
logger -t $myname"[$$]" -p user.info "exit 1"
exit 1

# Revision History
#
# $Log: RunScpTeeToDcc,v $
# Revision 1.5  2008/11/04 19:07:00  dechavez
# fixed debug typo
#
# Revision 1.4  2008/11/04 19:05:41  dechavez
# added option for second destination
#
# Revision 1.3  2008/03/13 22:14:12  dechavez
# remove instance checking (moved to ScpTeeToDcc)
#
# Revision 1.2  2008/03/10 21:40:24  dechavez
# fixed incorrect "myname"
#
# Revision 1.1  2008/03/10 21:38:06  dechavez
# initial release
#
