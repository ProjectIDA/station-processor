#!/bin/csh -x
# $Id: mspStage,v 1.3 2002/03/19 01:37:35 dec Exp $
#
# Helper code sourced by mspZipTeeFiles to do the compression
# We are in the $ZipDir/YYYY/DDD/HH directory when this code is executed

    set CurrentDir = `pwd`
    set dummy = Dummy.gz

StartOfStage:
    cd $CurrentDir
    set OutputDir  = $StageDir/$year/$day/$hr
    if (! -e $OutputDir) then
        set command = "mkdir -p $OutputDir"
        if ($debug) echo $command
        if ($debug) logger -t $myname"[$$]" -p user.info "$command"
         $command
        if ($status != 0) then
            echo "FAILED: $command"
            logger -t $myname"[$$]" -p user.info "FAILED"
            set exitstatus = 1
            goto EndOfStage
        endif
    endif

    @ BytesUsed  = `du -sk $StageDir | awk '{print $1}'`
    @ BytesUsed *= 1024 # convert from K-bytes to bytes
    if ($BytesUsed > $MaxBytesPerArchive) then
        logger -t $myname"[$$]" -p user.info "WARNING: $StageDir is too full"
        source $mspCreateIsoImage
        if ($exitstatus != 0) goto EndOfStage
        goto StartOfStage
    endif
    @ AvailSpace = $MaxBytesPerArchive - $BytesUsed

    # Move files from the compressed directory to the staging directory

    set message = "staging files from $CurrentDir to $OutputDir"
    if ($debug) then
        echo "$message"
        logger -t $myname"[$$]" -p user.info "$message"
    endif
    touch $dummy
    foreach file (`ls *.gz`)
        if ($file != $dummy) then
            @ FileSize = `wc -c $file | awk '{print $1}'`
            if ($FileSize > $AvailSpace) then
                logger -t $myname"[$$]" -p user.info "$StageDir is full"
                source $mspCreateIsoImage
                if ($exitstatus != 0) goto EndOfStage
                goto StartOfStage
            else
                set command = "mv $file $OutputDir"
                if ($debug) then
                    echo $command
                    logger -t $myname"[$$]" -p user.info "$command"
                endif
                $command
                if ($status == 0) then
                    @ AvailSpace -= $FileSize
                    if ($AvailSpace <= 0) then
                        logger -t $myname"[$$]" -p user.info "$StageDir is full"
                        source $mspCreateIsoImage
                        if ($exitstatus != 0) goto EndOfStage
                        goto StartOfStage
                    endif
                else
                    echo "FAILED: Rcommand"
                    logger -t $myname"[$$]" -p user.info "FAILED: $command"
                    set exitstatus = 1
                    goto EndOfStage
                endif
            endif
        endif
    end

    set exitstatus = 0

EndOfStage:

    cd $CurrentDir
    rm -f $dummy
    if ($debug) echo "msgStage exitstatus = $exitstatus"

# Revision History
#
# $Log: mspStage,v $
# Revision 1.3  2002/03/19 01:37:35  dec
# first working version
#
# Revision 1.2  2002/03/18 23:39:57  dec
# added cleanup and helper task error detection
#
# Revision 1.1  2002/03/18 22:48:50  dec
# created
#
