#!/bin/csh
# $Id: nrts_flushlog,v 1.5 2005/10/26 23:44:50 dechavez Exp $
#
#  Roll the NRTS log file(s). 

# General setup

set myname = nrts_flushlog

set setup  = ~nrts/scripts/ScriptSetup
if (! -e $setup ) then
    echo "${myname}: missing $setup"
    goto failure
endif
source $setup

set mode      = 644
set adm       = nrtsmgr
set mail      = /usr/ucb/Mail
set tmp       = /tmp/$myname.$$
set nrtslog   = $NRTS_HOME/log/nrtslog
set yyyyddd   = `gmt yyyyddd`
set Systems   = $NRTS_HOME/etc/Systems
set Processes = $NRTS_HOME/etc/Processes

# Save existing log file and make an empty new one

set facility = user

logger -t $myname -p $facility.info -i "flushing log"
mv $nrtslog  $tmp
cp /dev/null $nrtslog
chmod $mode  $nrtslog

# Let syslogd know about the new log file

sudo /usr/bin/pkill -HUP syslogd

logger -t $myname -p $facility.info -i "log flushed"

# Split the saved log into pieces

# Process logs

set tempname = $Processes.`hostname`
if (-e $tempname) set Processes = $tempname
if (-e $Processes) then
    set list = `cat $Processes | stripcat`
    if ($status != 0) then
        echo "stripcat $Processes failed" | $mail -s "$myname failed" $adm
        exit 0
    endif
else
    set list = ""
endif

foreach process ($list)
    set logdir = $NRTS_HOME/log/$process
    mkdir -p $logdir >& /dev/null
    if (-e $logdir) then
        set log = $logdir/$yyyyddd
        touch $log
        fgrep -i $process $tmp >> $log
        chmod $mode $log
        find $logdir -mtime $FlushLogFindValue -a -exec rm -f {} \;
    endif
end

# System logs

set tempname = $Systems.`hostname`
if (-e $tempname) set Systems = $tempname
if (-e $Systems) then
    set list = `cat $Systems | stripcat`
    if ($status != 0) then
        echo "stripcat $Systems failed" | $mail -s "$myname failed" $adm
        exit 0
    endif
else
    set list = ""
endif

foreach sta ($list)
    set STA = `echo $sta | ucase`
    set logdir = $NRTS_HOME/$sta/log
    if (-e $NRTS_HOME/$sta) mkdir -p $logdir >& /dev/null
    if (-e $logdir) then
        set log = $logdir/$yyyyddd
        touch $log
        fgrep $STA\  $tmp >> $log
        chmod $mode $log
        find $logdir -mtime $FlushLogFindValue -a -exec rm -f {} \;
    endif
end

# Remove the saved log

rm -f $tmp

# Collect the saved email

set mailbox = $NRTS_HOME/log/email/recent
if (-e $mailbox) then
    set logdir = $NRTS_HOME/log/email
    if (! -e $logdir) mkdir -p $logdir
    set log = $logdir/$yyyyddd
    touch $log $mailbox
    mv $mailbox $tmp
    touch $mailbox
    chmod $mode $mailbox
    cat $tmp >> $log
    chmod $mode $log
    rm -f $tmp
    find $logdir -mtime $FlushLogFindValue -a -exec rm -f {} \;
endif

exit 0

# Revision History
#
# $Log: nrts_flushlog,v $
# Revision 1.5  2005/10/26 23:44:50  dechavez
# use ScriptSetup and FlushLogFindValue for purging old files
#
# Revision 1.4  2005/02/11 22:07:34  dechavez
# remove all the cross platform crap and use sudo pkill to HUP syslogd so
# that we don't have to run this as root
#
# Revision 1.1.1.1  2003/04/18 22:44:18  nrts
# initial (imported) release
#
