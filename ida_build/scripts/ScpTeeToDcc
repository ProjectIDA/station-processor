#!/bin/csh -f
# $Id: ScpTeeToDcc,v 1.4 2008/11/04 19:09:03 dechavez Exp $
#
# Compress and move ISI tee files from one site into DCC incoming area via scp

set debug  = 0
set myname = ScpTeeToDcc

if ($debug) logger -t $myname"[$$]" -p user.info "begin"

# General setup

set setup  = ~nrts/scripts/ScriptSetup
if (! -e $setup ) then
    set message = "missing $setup"
    logger -t $myname"[$$]" -p user.info "$message"
    echo "${myname}: $message"
    goto failure
endif
source $setup

# Command line setup

set error = 0
if ($#argv == 2) then
    set site = $argv[1]
    set dest = $argv[2]
else if ($#argv == 3) then
    set site  = $argv[1]
    set dest  = $argv[2]
    set dest2 = $argv[3]
else
    set error = 1
endif

if ($error) then
    set message = "bad command line, error $error"
    logger -t $myname"[$$]" -p user.info "$message"
    echo "${myname}: $message"
    echo "usage: $myname site user@host:path"
    goto failure
endif

# Get the full path names

set SiteDir  = $NRTS_HOME/$site
set TeeDir   = $SiteDir/tee
set StopFlag = $TeeDir/STOP
set LockFile = $TeeDir/lock

# Nothing to do if there isn't a tee directory

if (! -d $TeeDir) then
    if ($debug) logger -t $myname"[$$]" -p user.info "no such tee dir $TeeDir"
    goto success
endif

# Make sure no other instance of this script is running

filelock $LockFile $$
if ($status != 0) then
    set active = `cat $LockFile`
    if ($debug) logger -t $myname"[$$]" -p user.info "$site tee directory locked by process $active"
    goto success
endif

# Nothing to do if the STOP flag is set in the tee directory

if (-e $StopFlag) then
    if ($debug) logger -t $myname"[$$]" -p user.info "quit: $site STOP flag set"
    goto success
endif

# Get a list of all but the most recent uncompressed tee files

cd $TeeDir
set tmp = /tmp/$myname.$$
/usr/bin/ls 4??????????????????????? >& $tmp
if ($status != 0) then
    rm $tmp
    goto success # nothing to process
endif
set NumFiles = `cat $tmp | wc -l`
if ($NumFiles <= 1) then
    rm $tmp
    goto success
endif
@ NumFiles -= 1
set UncompressedFileList = `head -$NumFiles $tmp`
rm $tmp

# Compress each file in the list

if ($debug) logger -t $myname"[$$]" -p user.info "compressing files in $TeeDir"
if ($status == 0) then
    foreach file ($UncompressedFileList)
        set output = $file.gz
        rm -f $output
        set command = "gzip $file"
        if ($debug) logger -t $myname"[$$]" -p user.info "$command"
        $command
        if (-e $file && -e $output) then
            logger -t $myname"[$$]" -p user.info "$command failed"
            rm -f $output
            goto failure
        endif
    end
endif

# Copy zipped files into DCC area

foreach file (4???????????????????????.gz)
    set command = "scp -q $file $dest/$file"
    if ($debug) logger -t $myname"[$$]" -p user.info "$command"
    $command
    if ($status != 0) then
        logger -t $myname"[$$]" -p user.info "FAILED: $command"
        goto failure
    endif
	if ($?dest2) then
        set command = "scp -q $file $dest2/$file"
        if ($debug) logger -t $myname"[$$]" -p user.info "$command"
        $command
        if ($status != 0) then
            logger -t $myname"[$$]" -p user.info "FAILED: $command"
            goto failure
        endif
	endif
    rm $file
end

# Normal exit

success:
pkill -HUP -x ispd
if ($debug) logger -t $myname"[$$]" -p user.info "completed OK"
exit 0

failure:
pkill -HUP -x ispd
logger -t $myname"[$$]" -p user.info "exit 1"
exit 1

# Revision History
#
# $Log: ScpTeeToDcc,v $
# Revision 1.4  2008/11/04 19:09:03  dechavez
# removed debug non-delete
#
# Revision 1.3  2008/11/04 19:05:41  dechavez
# added option for second destination
#
# Revision 1.2  2008/03/13 22:13:45  dechavez
# use filelock for instance locking
#
# Revision 1.1  2008/03/10 21:31:31  dechavez
# initial release
#
