#!/bin/csh -f
# $Id: isiReportIsoStats,v 1.4 2008/03/19 19:44:25 dechavez Exp $
#
# Print summary of iso stats

set myname = isiReportIsoStats

# General setup

set setup  = ~nrts/scripts/ScriptSetup
if (! -e $setup ) then
    echo "missing $setup"
    goto failure
endif
source $setup

# Command line setup

set error = 0
if ($#argv == 0) then
    set SiteList = "$SystemsList"
else 
    set SiteList = "$argv"
endif

# Loop for each site

foreach site ($SiteList)

    # Get the full path names

    set SiteDir    = $NRTS_HOME/$site
    set IsoDir     = $SiteDir/iso
    set ConfigFile = $IsoDir/config
    set StageDir   = $IsoDir/stage

    # Nothing to do if there isn't a complete ISO tree

    if (! -d $IsoDir) continue
    if (! -e $ConfigFile) continue
    if (! -e $StageDir) continue

    source $ConfigFile
    set capacity = $DefaultMaxMbytes

    # Get the stats

    @ bytes  = `du -sk $StageDir | awk '{print $1}'`
    @ bytes /= 1024

    set files = `ls $StageDir | grep ".gz" | wc -l`
    set images = `ls $IsoDir | grep ".iso" | wc -l`
    @ percent = (100 * $bytes) / $capacity

    echo "${site}: $bytes Mbytes ($percent%) staged in $files files [ $images ISO images ready to copy ]"

EndOfLoop:
end

success:
exit 0

failure:
exit 1

# Revision History
#
# $Log: isiReportIsoStats,v $
# Revision 1.4  2008/03/19 19:44:25  dechavez
# say "copy" instead of "burn"
#
# Revision 1.3  2007/05/16 17:39:43  dechavez
# report for all sites (if multi-site)
#
# Revision 1.2  2005/10/12 00:25:52  dechavez
# include site name in report line
#
# Revision 1.1  2005/10/12 00:14:05  dechavez
# initial release
#
