#!/bin/csh -f
# $Id: isiGetIsoStats,v 1.3 2005/10/11 18:17:58 dechavez Exp $
#
# Generate cdrom stats for use by ispd

set debug  = 0
set myname = isiGetIsoStats

# General setup

set setup  = ~nrts/scripts/ScriptSetup
if (! -e $setup ) then
    set message = "missing $setup"
    logger -t $myname"[$$]" -p user.info "$message"
    echo "${myname}: $message"
    goto failure
endif
source $setup

# Command line setup

set error = 0
if ($#argv == 0) then
    set error = 1
else if ($#argv == 1) then
    set site = $DefaultSite
    set output = $argv[1]
else if ($#argv == 2) then
    set site = $argv[1]
    set output = $argv[2]
else
    set error = 2
endif

if ($error) then
    set message = "bad command line, error $error"
    logger -t $myname"[$$]" -p user.info "$message"
    echo "usage: $myname [ site ] OutputFile"
    goto failure
endif

# Get the full path names

set SiteDir    = $NRTS_HOME/$site
set IsoDir     = $SiteDir/iso
set ConfigFile = $IsoDir/config
set StageDir   = $IsoDir/stage

# Nothing to do if there isn't a complete ISO tree

if (! -d $IsoDir) then
    set message = "missing ISO dir $IsoDir"
    logger -t $myname"[$$]" -p user.info "$message"
    echo "${myname}: $message"
    goto failure
endif

if (! -e $ConfigFile) then
    set message = "missing ISO config file $ConfigFile"
    logger -t $myname"[$$]" -p user.info "$message"
    echo "${myname}: $message"
    goto failure
endif

if (! -e $StageDir) then
    set message = "missing $StageDir"
    logger -t $myname"[$$]" -p user.info "$message"
    echo "${myname}: $message"
    goto failure
endif

source $ConfigFile
set capacity = $DefaultMaxMbytes

# Get the stats

@ bytes  = `du -sk $StageDir | awk '{print $1}'`
@ bytes /= 1024

set files = `ls $StageDir | grep ".gz" | wc -l`
set images = `ls $IsoDir | grep ".iso" | wc -l`

rm -f $output
echo "$files $bytes $images $capacity" > $output
chmod 666 $output
if ($debug) logger -t $myname"[$$]" -p user.info "$files $bytes $images $capacity"

success:
if ($debug) logger -t $myname"[$$]" -p user.info "exit 0"
exit 0

failure:
logger -t $myname"[$$]" -p user.info "exit 1"
exit 1

# Revision History
#
# $Log: isiGetIsoStats,v $
# Revision 1.3  2005/10/11 18:17:58  dechavez
# removed requirement on location of starting directory
#
# Revision 1.2  2005/08/26 20:27:40  dechavez
# added ViaIsp support, get capacity from iso/config file
#
# Revision 1.1  2005/08/17 21:06:56  dechavez
# initial release
#
