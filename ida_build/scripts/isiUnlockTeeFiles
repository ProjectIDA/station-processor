#!/bin/csh
# $Id: isiUnlockTeeFiles,v 1.1 2005/09/15 15:45:08 dechavez Exp $
#
# Unlock (chmod)) any locked tee files.  Meant to be run at boot time.

set debug  = 0
set myname = isiUnlockTeeFiles

if ($debug) logger -t $myname"[$$]" -p user.info "begin"

# General setup

set setup  = ~nrts/scripts/ScriptSetup
if (! -e $setup ) then
    set message = "missing $setup"
    logger -t $myname"[$$]" -p user.info "$message"
    echo "${myname}: $message"
    goto failure
endif
source $setup

# Process each site in turn

if ($SystemsCount == 0) then
    if ($debug) logger -t $myname"[$$]" -p user.info "SystemCount is zero"
    exit 0
endif

foreach site ($SystemsList)

    # Nothing to do if there isn't a tee directory

    set SiteDir = $NRTS_HOME/$site
    set TeeDir = $SiteDir/tee
    if (! -d $TeeDir) continue

    # Otherwise blast everything into unlocked state

    set dummy = $TeeDir/$myname.$$
    touch $dummy
    chmod 644 $TeeDir/*
    rm -f $dummy

end

# Normal exit

success:
if ($debug) logger -t $myname"[$$]" -p user.info "completed OK"
exit 0

failure:
logger -t $myname"[$$]" -p user.info "exit 1"
exit 1

# Revision History
#
# $Log: isiUnlockTeeFiles,v $
# Revision 1.1  2005/09/15 15:45:08  dechavez
# initial release
#
